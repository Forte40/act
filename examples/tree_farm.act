%config,tree_farm,depth,width,spacing,wait_time%

(
  s1 ?Bd Md   -- check for sapling
  ( 
    ~Cd                -- check for dirt
    (s##=#a ?Ic>0)4,16 -- find free
    Dd s1 Bd s#a       -- place dirt
    (?Ic>0 t1)         -- move replaced block
  )
  Mu s2 Bd    -- plant sapling
)=%plant%

(
  Dd              -- chop down 1
  (?Cu Mu         -- chop up
    (?If<1000 e1) -- refuel if needed
  )*=#h
  d#h             -- go back down
)=%chop%

(
  ?If<1000
  ?%peripheral,match,top,furnace%
  Gf rr Gu2 Gf   -- move above furnace
  (s## Ed)4,16   -- drop logs in
  Gb Gd          -- move in front
  (s## ~Ef1)3,16 -- drop 1 fuel in
  Gd Gf ll       -- move under furnace
  z80 s2 Su
  s3 (?Ic>1 e1)*  -- eat all but one
)=%refuel%

(
  Mf#C
  (s## ?Ic>0)4,16=#a -- select free
  Mf
  (?Ic>0
    (?Ic>0 t##)4,#a=#b -- consolidate
    (?Ic==0 s#b)
    %chop%
  )
)=%nexttree%

(
  r                  -- face sapling chest
  (s##=#a ?Ic>0)3,16 -- get free spot
  (
    s2 ?Is>0 ?Sf     -- while need stuff and can get stuff
    s#a (?Ic>0 rEfl) -- drop excess in other chest
  )*
  r s#a
  (
    ?Sf ?Ic>0 lEfr   -- put stuff back in chest
  )*
  l2
)=%getsapling%

%request,64,dirt,1%
%getsapling%
%request,64,saplings,2%

(
  %request,16,dirt,1
          ,16,saplings,2%
  Gf u
  %nexttree%
  (
    (
      (%plant% / %nexttree%)#A
      / r %nexttree% r
    )2 
    / l %nexttree% l
  )#B

  Mf#C Mf r (Mf#CMf/Mf#CMf)#B r d Gb
  %refuel%
  l (s## (?Ic>0 Ef))4,16 r -- drop off logs
  z#D
)*