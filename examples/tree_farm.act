-- setup

w<home>           -- set home position
Df c e Gf          -- dig under tree and fuel up
(?Hu Mu)* (~Hd Gd)* -- get logs and return
                  -- make chest, eat the rest
c t2,1 t3,1 t5,1 t7,1 t9,1 t10,1 t11,1 s16 c s1 e

-- vacuum routines

((~Hd Sd Gd)* (~Hf Sf Gu) f)=%Vf% -- vacuum forward

(
	(%Vf%3 r)2             -- position at corner
	(
	  (%Vf%7 / r %Vf% r) 2 -- vacuum 2 rows
	  / l %Vf% l         
	) 4                    -- 4 times
	r %Vf%4 r %Vf%2        -- return to home
)=%vacuum%

rr z70 s15 %vacuum%     -- wait for leaf decay and vacuum for saplings
Pf                   -- plant first tree
s14 Dd s16 Pd s15 Ed -- place and fill sapling chest

-- dump extra stuff
rr Gf7 s14 Ef s1 Ef s2 Ef s3 Ef s4 Ef rr Gf7